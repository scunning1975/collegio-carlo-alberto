---
output: github_document
---

```{r setup, include = F}
# devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```

# Lalonde

The National Supported Work (NSW) Demonstration dataset is one of the most commonly used dataset in econometrics based on [Lalonde (1986)](https://business.baylor.edu/scott_cunningham/teaching/lalonde-1986.pdf) and [Dehejia and Wahba (2002)](https://business.baylor.edu/scott_cunningham/teaching/dehejia-and-wahba-2002.pdf). Both the original 1986 article by Lalonde as well as the follow-up articles by Dehejia and Wahba used the data to evaluate contemporary approaches to causal inference using non-experimental data. Cleverly, they start with an experimental dataset to establish 'true' causal effect and then use a non-experimental dataset consisting of a control group of randomly sampled American households to see if covariate adjustment can recover causal effects under fairly dramatic selection problems. 

In [Causal Inference 1](github.com/Mixtape-Sessions/Causal-Inference-1), we have already shown that the nonexperimental dataset suffers from severe negative selection into the job trainings program.  Simple comparisons often found negative effects of job training on earnings despite the fact the program had an average positive effect of around $1700 higher real earnings in 1978. That lab found that some selection on observable methods like propensity score weighting and nearest neighbor matching recovered causal effects close to that found using experimental data. 

In this lab, we will study the performance of several difference-in-differences estimators using both the experimental and non-experimental datasets. 

1. We will first perform analysis on the experimental dataset `https://raw.github.com/Mixtape-Sessions/Causal-Inference-2/master/Labs/Lalonde/lalonde_exp_panel.dta`

```{stata, label = load-exp-data, collectcode = TRUE}
* ssc install drdid, replace
clear
use "https://raw.github.com/Mixtape-Sessions/Causal-Inference-2/master/Lab/Lalonde/lalonde_exp_panel.dta", clear 
```

a. Under random assignment, the simple difference-in-means identifies the ATE, and since the original NSW was a randomized experiment, we can do this.  Calculate the simple difference-in-means on the experimental dataset to estimate the "treatment effect" two separate ways: (1) manually calculate averages for both treatment (`ever_treated=1`) and control (`ever_treated=0`) and use them to estimate the returns to the program, and (2) estimate the effect with an OLS specification. In both cases, use only the year `78` and `re` variable for real earnings. 

```{stata diff-in-means-exp}
* Difference-in-means - Averages
summarize re if ever_treated==1 & year==78 // $6349.144
summarize re if ever_treated==0 & year==78 // $4554.801

display 6349.144 - 4554.801

* Difference-in-means - OLS
reg re i.ever_treated if year == 78, robust
```

b. Estimate the effect of the treatment, `ever_treated`, on real earnings, `re`, in a difference-in-differences estimator using years `78` for post period and `75` as the pre-period (ignoring for now year `74`). As with 1a, do this in the following two ways: (1) manually calculate the four means you need for the DiD equation and then estimate using the DiD equation, and (2) estimate the ATT using the OLS specification for the DiD equation with robust standard errors. Reminder to only use `78` and `75` (i.e., do not include `74` in OLS analysis). 

```{stata diff-in-diff-exp}
* Difference-in-differences - Averages
egen y00=mean(re) if ever_treated==0 & year==75
egen y01=mean(re) if ever_treated==0 & year==78
egen y10=mean(re) if ever_treated==1 & year==75
egen y11=mean(re) if ever_treated==1 & year==78

egen ey00 = max(y00)
egen ey01 = max(y01)
egen ey10 = max(y10)
egen ey11 = max(y11)

gen did = (ey11 - ey10) - (ey01 - ey00)
sum did
drop y00-did

* Difference-in-differences - OLS
gen post 		= 0 if year==75
replace post 	= 1 if year==78
reg re ever_treated##post, robust 
```

c. Check the pre-trends for 1974 relative to 1975 two ways: (1) manually calculate the DiD equation on 1974 relative to 1975 and (2) estimate the dynamic OLS specification with an interaction of `ever_treated` with `74`, an interaction of `ever_treated` with `78`.  Compare your answers for 2c to what you found in 2a and 2b. 

```{stata pre-trends-exp}

* Event study and pre-trends using manually calculated averages
egen y00=mean(re) if ever_treated==0 & year==75
egen y01=mean(re) if ever_treated==0 & year==74
egen y10=mean(re) if ever_treated==1 & year==75
egen y11=mean(re) if ever_treated==1 & year==74

egen ey00 = max(y00)
egen ey01 = max(y01)
egen ey10 = max(y10)
egen ey11 = max(y11)

gen did = (ey11 - ey10) - (ey01 - ey00)
sum did
drop y00-did post

* Event study and pre-trends using OLS 
gen pre = 0
replace pre=1 if year==74
gen post = 0 
replace post = 1 if year==78

reg re ever_treated##post ever_treated##pre, robust
```

2. Now, we turn to the non-experimental dataset `https://raw.github.com/Mixtape-Sessions/Causal-Inference-2/master/Labs/Lalonde/lalonde_nonexp_panel.dta`. 

```{stata load-nonexp-data, collectcode = T}
use "https://raw.github.com/Mixtape-Sessions/Causal-Inference-2/master/Lab/Lalonde/lalonde_nonexp_panel.dta", clear 
```

a. Repeat 1a (simple difference-in-means for `78` only), 1b (DiD using manual calculations and OLS specification for `78` and `75` only) and 1c (event study calculations manually and dynamic OLS specification for `78`, `75` and `74`)

```{stata diff-in-means-nonexp}
* Difference-in-means - Averages
summarize re if ever_treated==1 & year==78 // $6349.144
summarize re if ever_treated==0 & year==78 // $4554.801

display 6349.144 - 4554.801

* Difference-in-means - OLS
reg re i.ever_treated if year == 78, robust
```

```{stata diff-in-diff-nonexp}
* Difference-in-differences - Averages
egen y00=mean(re) if ever_treated==0 & year==75
egen y01=mean(re) if ever_treated==0 & year==78
egen y10=mean(re) if ever_treated==1 & year==75
egen y11=mean(re) if ever_treated==1 & year==78

egen ey00 = max(y00)
egen ey01 = max(y01)
egen ey10 = max(y10)
egen ey11 = max(y11)

gen did = (ey11 - ey10) - (ey01 - ey00)
sum did
drop y00-did

* Difference-in-differences - OLS
gen post 		= 0 if year==75
replace post 	= 1 if year==78
reg re ever_treated##post, robust 
```

```{stata pre-trends-nonexp}

* Event study and pre-trends using manually calculated averages
egen y00=mean(re) if ever_treated==0 & year==75
egen y01=mean(re) if ever_treated==0 & year==74
egen y10=mean(re) if ever_treated==1 & year==75
egen y11=mean(re) if ever_treated==1 & year==74

egen ey00 = max(y00)
egen ey01 = max(y01)
egen ey10 = max(y10)
egen ey11 = max(y11)

gen did = (ey11 - ey10) - (ey01 - ey00)
sum did
drop y00-did post

* Event study and pre-trends using OLS 
gen pre = 0
replace pre=1 if year==74
gen post = 0 
replace post = 1 if year==78

reg re ever_treated##post ever_treated##pre, robust
```

b. Repeat 1b and 1c (OLS specifications) controlling linearly for `age, agesq, agecube, educ, educsq, marr, nodegree, black, hisp` with robust standard errors.

```{stata, include = F, collectcode = T}
gen pre = 0
replace pre=1 if year==74
gen post = 0 
replace post = 1 if year==78
```

```{stata twfe-w-covariates, collectcode = T}
reg re ever_treated##post age agesq agecube educ educsq marr nodegree black hisp, robust
```

c. Use the `DRDID` command to estimate a doubly-robust difference-in-differences with covariates `age + agesq + agecube + educ + educsq + marr + nodegree + black + hisp`, `id` panel unit identifier, `year` as the panel time identifier, and reporting the outcome regression analysis [(Heckman, Ichimura and Todd 1997)](http://jenni.uchicago.edu/papers/Heckman_Ichimura-Todd_REStud_v64-4_1997.pdf), inverse probability weight estimator [(Abadie 2005)](https://academic.oup.com/restud/article-abstract/72/1/1/1581053?redirectedFrom=fulltext), doubly robust [(Sant'anna and Zhao 2020)](https://www.sciencedirect.com/science/article/abs/pii/S0304407620301901).  Compare these results with 1a, 1b, 2a and 2b. 

```{stata drdid}
* Doubly-robust DID
drdid re age agesq agecube educ educsq marr nodegree black hisp re74 u74 if year == 75 | year == 78, time(year) ivar(id) tr(ever_treated) all
```

